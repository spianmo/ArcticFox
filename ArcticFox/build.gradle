apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.kezong.fat-aar'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 31

    defaultConfig {
        //applicationId "com.kirito.basic"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures{
        viewBinding = true
    }
    libraryVariants.all { variant ->  //module工程则为libraryVariants，主项目则是applicationVariants
        variant.javaCompileProvider.get().doLast {
            println("start classes obfuscation " + "${javaCompile.destinationDir}")
            javaexec {
                main = "-jar"
                args = [
                        "-Dfile.encoding=utf-8",
                        "../obfuseStringGradle.jar",
                        project.name,
                        variant.javaCompile.destinationDir
                ]
            }
        }
    }
    fataar{
        transitive = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    api 'com.github.vokod:SettingViews:1.0.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    api 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'
    api  'com.zhy:okhttputils:2.6.2'
    api 'com.jonathanfinerty.once:once:1.3.1'
    api 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    api group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    api group: 'org.jsoup', name: 'jsoup', version: '1.14.3'
    api "com.github.PhilJay:MPAndroidChart:v3.1.0"
    api 'com.github.kongzue:DialogX:0.0.43.beta6'
    api 'com.tencent:mmkv-static:1.2.11'
    embed project (path: ':Protocol', configuration: 'default')
    implementation('com.jrummyapps:android-shell:1.0.1@aar') {
        transitive = true;
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compileOnly 'de.robv.android.xposed:api:53'
    api 'com.tencent.bugly:crashreport:3.4.4'
    api 'com.tencent.bugly:nativecrashreport:3.9.2'
    api 'com.github.bumptech.glide:glide:4.12.0'
}


afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.kirito.basic'
                artifactId = 'ArcticFox'
                version = '1.0.4'
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.kirito.basic'
                artifactId = 'ArcticFox-debug'
                version = '1.0.4'
            }
        }
    }
}